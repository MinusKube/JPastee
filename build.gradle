plugins {
    id 'com.github.johnrengelman.shadow' version '2.0.1'
}

group 'fr.minuskube'
version '1.0.1'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.mashape.unirest:unirest-java:1.4.9'

    testCompile 'junit:junit:4.12'
}

jar { archiveName = 'JPastee-' + project.version + '.jar' }
shadowJar { archiveName = 'JPastee-' + project.version + '-shaded.jar' }



task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts { archives javadocJar, sourcesJar }

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'JPastee'
                packaging 'jar'
                description 'Java Wrapper for the https://paste.ee/ API'
                url 'https://github.com/MinusKube/JPastee'

                scm {
                    connection 'scm:git:git://github.com/MinusKube/JPastee.git'
                    developerConnection 'scm:git:ssh://github.com:MinusKube/JPastee.git'
                    url 'http://github.com/MinusKube/JPastee/tree/master'
                }

                licenses {
                    license {
                        name 'GNU General Public License v3.0'
                        url 'https://www.gnu.org/licenses/gpl-3.0.en.html'
                    }
                }

                developers {
                    developer {
                        id 'minuskube'
                        name 'MinusKube'
                        email 'minuskube@gmail.com'
                    }
                }
            }
        }
    }
}